plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'com.gradle.plugin-publish' version '0.16.0'
    id 'com.palantir.git-version' version '0.12.3'
    id "maven-publish"
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    jcenter()
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()

    implementation "dev.guardrail:guardrail_2.12:0.68.0"
    implementation "dev.guardrail:guardrail-cli_2.12:0.65.2"
    implementation "dev.guardrail:guardrail-core_2.12:0.65.4"
    implementation "dev.guardrail:guardrail-java-async-http_2.12:0.65.2"
    implementation "dev.guardrail:guardrail-java-dropwizard_2.12:0.65.2"
    implementation "dev.guardrail:guardrail-java-spring-mvc_2.12:0.65.2"
    implementation "dev.guardrail:guardrail-java-support_2.12:0.65.2"
    implementation "dev.guardrail:guardrail-scala-akka-http_2.12:0.65.2"
    implementation "dev.guardrail:guardrail-scala-dropwizard_2.12:0.65.2"
    implementation "dev.guardrail:guardrail-scala-endpoints_2.12:0.65.2"
    implementation "dev.guardrail:guardrail-scala-http4s_2.12:0.65.4"
    implementation "dev.guardrail:guardrail-scala-support_2.12:0.65.2"

    testImplementation('org.spockframework:spock-core:1.3-groovy-2.5') {
        exclude module: 'groovy-all'
    }
    testImplementation 'cglib:cglib-nodep:3.3.0'
}

group = 'dev.guardrail'

version = gitVersion().substring(1) // gradle-git-version does not support leading `v` in `v0.1.2`: https://github.com/palantir/gradle-git-version/issues/72

description = 'guardrail OpenAPI code generator plugin'

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    classifier "sources"
    extension "jar"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    classifier "javadoc"
    extension "jar"
}

publishing {
    publications {
        mainMaven(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}


gradlePlugin {
    plugins {
        guardrailPlugin {
            id = project.group
            implementationClass = 'dev.guardrail.GuardrailGradlePlugin'
        }
    }
}

// Add a source set for the functional test suite
sourceSets {
    functionalTest {
    }
}

gradlePlugin.testSourceSets(sourceSets.functionalTest)
configurations.functionalTestImplementation.extendsFrom(configurations.testImplementation)

// Add a task to run the functional tests
task functionalTest(type: Test) {
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
}

check {
    // Run the functional tests as part of `check`
    dependsOn(tasks.functionalTest)
}

pluginBundle {
  website = 'https://guardrail.dev/'
  vcsUrl = 'https://github.com/guardrail-dev/guardrail'
  description = 'Principled code generation from OpenAPI specifications'
  tags = ['openapi', 'swagger', 'codegen', 'scala', 'java']

  plugins {
    guardrailPlugin {
      displayName = 'guardrail gradle plugin'
    }
  }
}
